#!/usr/bin/env perl

use strict;
use warnings;

use AnyEvent;
use AnyEvent::IRC::Client;
use HTTP::Cookies;
use IO::File;
use WWW::Mechanize;
use YAML qw(LoadFile);

my %DISPATCH_IGNORE = map { $_, 1 } qw(
  JOIN
  MODE
  NOTICE
  PING
  PONG
  PRIVMSG
  QUIT
);
my %UNITS = (
    KB => 1024,
    MB => 1024 * 1024,
    GB => 1024 * 1024 * 1024,
);

my $config = LoadFile($ENV{HOME}.'/.torrent-watcher.yaml');

use Data::Dumper;
#warn Dumper $config;

my $cookies = HTTP::Cookies->new;
foreach my $key (qw(uid pass)) {
    $cookies->set_cookie(1, $key, $config->{feeds}{bitmetv}{$key}, '/', 'www.bitmetv.org', '80');
}
my $mech = WWW::Mechanize->new(
    cookie_jar => $cookies
);

my $c = AnyEvent->condvar;

my $timer;
my $con = AnyEvent::IRC::Client->new;

$con->reg_cb(
    connect => sub {
        my ($con, $err) = @_;
        if (defined $err) {
            warn "connect error: $err\n";
            return;
        }
        debug('connected');
    },
    registered => sub {
        my ($con) = @_;
        debug('registered');
        send_for_invite($con);
    },
    disconnect => sub {
        my ($con) = @_;
        debug('disconnected');
    },
);

$con->reg_cb(
    debug_send => sub {
        my ($con, @params) = @_;
        #warn 'SEND: ',Dumper \@params;
    },
    debug_recv => sub {
        dispatch(@_);
    }
);

$con->reg_cb(
    privatemsg => sub { handle_message(@_) },
    publicmsg  => sub { handle_message(@_) },
);

$con->enable_ssl;
irc_connect($con);
# turn on ping
# dis/reconnect if ping times out
$con->enable_ping(
    300,
    sub {
        my ($con) = @_;
        $con->disconnect;
        irc_connect($con);
    }
);

$c->wait;

exit;

sub debug {
    warn scalar localtime(time),': ', @_,"\n";
}

sub irc_connect {
    my ($con) = @_;
    $con->connect(
        $config->{irc}{server},
        $config->{irc}{port},
        {   nick     => $config->{irc}{nick},
            password => $config->{irc}{password},
            user     => $config->{irc}{name}
        }
    );
}

sub send_for_invite {
    my ($con) = @_;
    debug('sending for invite');
    $con->send_srv(
        PRIVMSG => $config->{irc}{botname},
        '!invite ' . $config->{irc}{nick} . ' ' . $config->{irc}{irckey}
    );
}

sub dispatch {
    my($con, $msg) = @_;
    my $cmd = $msg->{command};
    if ($cmd eq 'INVITE') {
        process_invite($con, $msg);
    }
    elsif ($DISPATCH_IGNORE{$cmd}) {
        # do nothing on commands we've configured to ignore
    }
    elsif ($cmd =~ m/^\d+$/) {
        # do nothing on numeric commands
    }
    else {
        warn 'UNRECOGNIZED: ', Dumper $msg;
    }
}

sub process_invite {
    my($con, $msg) = @_;
    my $channel = $msg->{params}[1];
    if ($config->{irc}{channels}{$channel}) {
        debug('accepting invite to ',$channel);
        $con->send_srv('JOIN', $channel);
    }
    else {
        debug('not interested in ',$channel);
    }
}

sub handle_message {
    my($con, $to, $msg) = @_;

    return unless $msg->{params}[0] eq '#bitmetv.announce';
    my($type, $rest) = split(/\s/, $msg->{params}[1], 2);
    return unless $type and $type eq 'BitMeTV.ORG';

    my %data = ($rest =~ m/(\S+):\s+\[\s+(.+?)\s+\]/g);
    debug 'saw ',$data{Torrents};

    # check exclusions
    foreach my $test (keys %{$config->{feeds}{bitmetv}{exclude}}) {
        next unless $config->{feeds}{bitmetv}{exclude}{$test};
        if ($data{Torrents} =~ m/$test/i) {
            debug 'excluding [',$test,'] ',$data{Torrents};
            return;
        }
    }

    # check positive regexes
    my $size = bytes($data{Size});
    foreach my $test (keys %{$config->{feeds}{bitmetv}{regexes}}) {
        next unless $data{Torrents} =~ m/$test/i;
        my $limit = $config->{feeds}{bitmetv}{regexes}{$test};
        my $bytes = bytes($limit);
        if ($size > $bytes) {
            debug 'would fetch ', $data{Torrents}, ' but ', $data{Size}, ' > ', $limit;
            return;
        }
        return fetch_torrent($data{Link});
    }

    return;
}

sub bytes {
    my $str = shift;
    my($num, $unit) = split(/\s/, $str);
    return $num * $UNITS{$unit};
}

sub fetch_torrent {
    my($page) = @_;
    debug 'grabbing ', $page;
}
